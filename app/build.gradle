apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.wchristiansen.assignmenttracker"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    project.afterEvaluate {
        // Copies the debug version of an apk into a directory added to version control
        applicationVariants.all { variant ->
            // Only copy debug build variants for now
            if (variant.name in ["debug"]) {
                def backupFolder = rootProject.file("archive")
                def backName = String.format("%s_v%s_(%d).apk", rootProject.name, variant.versionName, variant.versionCode)
                def backupFile = new File(backupFolder, backName)

                variant.outputs.all { output ->
                    Task copyApkTask = project.task("copy${variant.name.capitalize()}Apk", type: Copy) {
                        from output.outputFile.getParent()
                        into backupFolder
                        include output.outputFileName
                        rename(output.outputFileName, backupFile.getName())
                    }

                    // Run this task once the assemble task is finished
                    variant.assemble.finalizedBy(copyApkTask)
                }
            }
        }
    }
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })

    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    debugCompile 'com.amitshekhar.android:debug-db:1.0.1'

    testCompile 'junit:junit:4.12'
}
